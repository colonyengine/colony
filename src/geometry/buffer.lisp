(in-package #:virality)

(defgeneric get-geometry-buffer-names (spec geometry-group))

(defmethod get-geometry-buffer-names (spec (group geometry-group/interleaved))
  (list (name group)))

(defmethod get-geometry-buffer-names (spec (group geometry-group/separate))
  (let (names)
    (u:do-hash-keys (k (attributes group))
      (push (u:format-symbol :keyword "~a/~a" (name group) k) names))
    (nreverse names)))

(defun make-geometry-buffers (geometry)
  (let ((layout (layout geometry)))
    (setf (buffers geometry) (make-array 0 :fill-pointer 0 :adjustable t))
    (dolist (group-name (group-order layout))
      (let ((group (u:href (groups layout) group-name)))
        (dolist (name (get-geometry-buffer-names (layout geometry) group))
          (let ((buffer (gl:gen-buffer)))
            (setf (u:href (buffer-names geometry) name) buffer)
            (vector-push-extend buffer (buffers geometry))))))))

(defun configure-geometry-buffers (geometry)
  (let ((layout (layout geometry))
        (buffer-offset 0)
        (attribute-offset 0))
    (dolist (group-name (group-order layout))
      (let* ((group (u:href (groups layout) group-name))
             (buffer-count (get-geometry-group-buffer-count group))
             (group-buffers (make-array
                             buffer-count
                             :displaced-to (buffers geometry)
                             :displaced-index-offset buffer-offset))
             (attribute-count (length (attribute-order group))))
        (dotimes (i attribute-count)
          (gl:enable-vertex-attrib-array (+ attribute-offset i)))
        (configure-geometry-group group attribute-offset group-buffers)
        (incf buffer-offset buffer-count)
        (incf attribute-offset attribute-count)))))

(defun get-geometry-buffer-size (buffer)
  (* (length buffer)
     (etypecase buffer
       ((simple-array u:b8 *) 1)
       ((simple-array u:ub8 *) 1)
       ((simple-array u:b16 *) 2)
       ((simple-array u:ub16 *) 2)
       ((simple-array u:b32 *) 4)
       ((simple-array u:ub32 *) 4)
       ((simple-array single-float *) 4)
       ((simple-array double-float *) 8))))

(defmacro with-geometry-buffer ((ptr size vector) &body body)
  (u:with-gensyms (sv)
    `(static-vectors:with-static-vector
         (,sv (length ,vector)
              :element-type (array-element-type ,vector)
              :initial-contents ,vector)
       (let ((,size (get-geometry-buffer-size ,vector))
             (,ptr (static-vectors:static-vector-pointer ,sv)))
         ,@body))))

(defun fill-geometry-buffer (geometry buffer-name data
                             &key (usage :dynamic-draw))
  (let ((data (u:flatten-numbers data)))
    (with-geometry-buffer (ptr size data)
      (let ((buffer (u:href (buffer-names geometry) buffer-name)))
        (gl:bind-buffer :array-buffer buffer)
        (%gl:buffer-data :array-buffer size ptr usage)
        (gl:bind-buffer :array-buffer 0)))))
